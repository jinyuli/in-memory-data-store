plugins {
    id 'java'
    id 'checkstyle'
    id 'application'
}

//name 'Jimds'
group 'com.codeloam.memory.store'
version '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_19
    targetCompatibility = JavaVersion.VERSION_19
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll(["--enable-preview"])
}

sourceSets {
    integrationTest {
        java
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

application {
    mainClass = 'com.codeloam.memory.store.Main'
}

jar {
    archiveBaseName = 'Jimds'
    manifest {
        attributes(
                "Main-Class": "com.codeloam.memory.store.Main",
                "Package": "com.codeloam.memory.store",
                "Implementation-Version": "${project.version}"
        )
    }
}

test {
    testLogging {
        events "passed", "failed", "skipped"
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed", "failed", "skipped"
    }
}

tasks.register("uberJar", Jar) {
    archiveBaseName = 'Jimds'
    archiveClassifier = 'uber'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar')}.collect{ zipTree(it) }
    }
}

tasks.register("integrationJar", Jar) {
    archiveBaseName = 'Integration'
    manifest {
        attributes(
                "Main-Class": "com.codeloam.memory.store.PressureTest",
                "Package": "com.codeloam.memory.store",
                "Implementation-Version": "${project.version}"
        )
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.output)
    from(sourceSets.integrationTest.output)
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar')}.collect{ zipTree(it) }
    }
    dependsOn configurations.integrationTestRuntimeClasspath
    from {
        configurations.integrationTestRuntimeClasspath.findAll { it.name.endsWith('jar')}.collect{ zipTree(it) }
    }
}

dependencies {
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.5.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.1.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    integrationTestAnnotationProcessor group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.36'
    integrationTestAnnotationProcessor group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.36'
    integrationTestImplementation group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.36'
    integrationTestImplementation group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.36'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    integrationTestImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.1.1'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    integrationTestImplementation group: 'org.openjdk.jol', name: 'jol-core', version: '0.16'
    integrationTestImplementation group: 'com.microsoft.gctoolkit', name: 'gctoolkit-api', version: '3.0.0'
    integrationTestImplementation group: 'com.microsoft.gctoolkit', name: 'gctoolkit-vertx', version: '3.0.0'
    integrationTestImplementation group: 'com.microsoft.gctoolkit', name: 'gctoolkit-parser', version: '3.0.0'
}

test {
    useJUnitPlatform()
}