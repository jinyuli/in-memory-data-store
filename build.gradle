plugins {
    id 'java'
    id 'checkstyle'
    id 'application'
}

//name 'Jimds'
group 'com.codeloam.memory.store'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    integrationTest {
        java
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

application {
    mainClass = 'com.codeloam.memory.store.Main'
}

jar {
    archiveBaseName = 'Jimds'
    manifest {
        attributes(
                "Main-Class": "com.codeloam.memory.store.Main",
                "Package": "com.codeloam.memory.store",
                "Implementation-Version": "${project.version}"
        )
    }
}

test {
    testLogging {
        events "passed", "failed", "skipped"
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed", "failed", "skipped"
    }
}

check.dependsOn integrationTest

//repositories {
//    mavenCentral()
//}

//configurations {
//    intTestImplementation.extendsFrom implementation
//    intTestRuntimeOnly.extendsFrom runtimeOnly
//}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.1.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    integrationTestImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.1.1'
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    integrationTestImplementation group: 'org.openjdk.jol', name: 'jol-core', version: '0.16'
}

test {
    useJUnitPlatform()
}